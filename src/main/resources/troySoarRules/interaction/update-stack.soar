##!
# @file
#
# @created smohan 20170804


sp {interaction*apply*update-stack*add-segment*only*message
	(state <s>	^name interaction
            	^operator <op>
             	^interaction.stack <stack>
             	^new-segment <new-seg>
              	-^terminate-segment <seg>)
 	(<stack> ^segment <seg>)
 	(<op>	^name update-stack)
  	(<new-seg>	^message <m1>)
-->
	(<new-seg>	^next <seg>)
 	(<stack>	^segment <new-seg>	
           		^segment <seg> -)
  	(<m1>	^processed true)
  
  }

sp {interaction*apply*update-stack*add-segment*only*status
	(state <s>	^name interaction
            	^operator <op>
             	^interaction <inter> 
             	^new-segment <new-seg>
              	-^terminate-segment <seg>)
 	(<inter>	^stack <stack>)
 	(<stack> ^segment <seg>)
 	(<op>	^name update-stack)
  	(<new-seg>	^processing-status <ps>)
-->
	(<new-seg>	^next <seg>)
 	(<stack>	^segment <new-seg>	
           		^segment <seg> -)
  }

sp {interaction*apply*update-stack*terminate-segment*only*status
	(state <s>	^name interaction
            	^operator.name update-stack
             	^interaction <inter>
              	^terminate-segment <seg>
               	-^new-segment <new-seg>)
 	(<inter>	^stack <stack>)
 	(<stack>	^segment <seg>)
  	(<seg>	^next <next-seg>)
-->
	(<stack>	^segment <next-seg>
          		^segment <seg> -)
  }

sp {interaction*apply*update-stack*add-segment*terminate-previous*message
	(state <s>	^name interaction
            	^operator <op>
             	^interaction.stack <stack>
              	^new-segment <new-seg>
               	^terminate-segment <seg>)
 	(<stack>	^segment <seg>)
  	(<op>	^name update-stack)
   	(<seg>	^next <next-seg>)
    (<new-seg>	^message <m1>)
-->
	(<new-seg>	^next <next-seg>)
 	(<stack>	^segment <new-seg>
           		^segment <seg> -)
  	(<m1>	^processed true)
  }

sp {interaction*apply*update-stack*delete-wmes
	(state <s>	^name interaction
            	^operator <op>
            	^delete-counter-wme <dm>)
 	(<op> ^name update-stack)
 	(<dm>	^_metadata <met>
        	^counter <count>)
-->
	(<met> ^counter <count> -)
 }

#sp {interaction*apply*update-stack*modification*replace-values
#	(state <s>	^name interaction
#            	^operator <op>
#             	^modify-segment <seg>
#              	^modifications <meg>
#               	^interaction.stack <stack>)
# 	(<meg>	^<arg> <val>)
#  	(<seg>	^<arg> <oval>)
#   	(<stack> ^segment <nseg>)
#-->
#	(<seg>	^<arg> <oval> -
#        	^<arg> <val>)
# }

sp {interaction*apply*update-stack*modification*add-values
	(state <s>	^name interaction
            	^operator.name update-stack
             	^modify-segment <seg>
              	^modifications <meg>
               	^interaction.stack <stack>)
 	(<meg>	^add.<arg> <val>)
-->
	(<seg>	^<arg> <val>)
 }

sp {interaction*apply*update-stack*modification*remove-values
	(state <s>	^name interaction
            	^operator.name update-stack
             	^modify-segment <seg>
              	^modifications <meg>
               	^interaction.stack <stack>)
 	(<meg>	^remove.<arg> <val>)
-->
	(<seg>	^<arg> <val> -)
 }

sp {interaction*apply*update-stack*remove-status
	(state <s>	^name interaction
            	^interaction <inter>
             	^operator.name update-stack)
 	(<inter> ^processing-status <ps>)
  	(<ps>	^id <any>)
-->
	(<inter> ^processing-status <ps> -
          	^processing-status <ps-new>)
 }