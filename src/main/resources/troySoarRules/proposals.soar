##!
# @file
#
# @created smohan 20170330

sp {topstate*propose*interaction*message
	(state <s> ^name troy
               ^io.input-link.interaction.message <message>
               ^topstate <s>)
    (<message> -^processed true)
-->
	(<s> ^operator <op> +)
 	(<op> 	^name interaction
        	^type incoming-message
         	^message <message>)
  }

sp {topstate*propose*interaction*status
	(state	<s>	^name troy
            	^interaction.processing-status <status>)
 	(<status> ^id <any-id>)
-->
	(<s>	^operator <op> +)
 	(<op>	^name interaction
        	^type processing-status
         	^status <status>)
  }

sp {topstate*propose*interaction*2
    (state <s> ^name troy
               ^interaction.stack.segment <segment>)
    (<segment> ^status complete)
-->
    (<s> ^operator <op> +)
    (<op> ^name interaction)
    }


sp {topstate*propose*comprehension
    (state <s> ^name troy
               ^interaction.stack.segment <seg>)
    (<seg> ^message <m>
           ^groundings <g>)
    (<g> -^<any>)
    (<m> ^processed true
         ^lexical <lex>)
-->
    (<s> ^operator <op> +)
    (<op> ^name comprehension
          ^message <lex>
          ^segment <seg>)
    }

sp {topstate*propose*generation
	(state <s>	^name troy
            	^interaction.stack.segment <seg>)
 	(<seg>	^purpose <purpose-type>
         	^information-type <i-type>
          	-^response-generated true)
-->
	(<s>	^operator <op> +)
 	(<op>	^name generation)
   }


sp {topstate*propose*action
    (state <s> ^name troy
               ^interaction <inter>)
    (<inter>	^stack.segment <seg>
             	-^processing-status.<status> <any>)
    (<seg> ^groundings <g>
           -^status complete)
    (<g> ^indexed-action <ia>)
-->
    (<s> ^operator <op> +)
    (<op> ^name action
          ^indexed-action <ia>
          ^segment <seg>)
    }