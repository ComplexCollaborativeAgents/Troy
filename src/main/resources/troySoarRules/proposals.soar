##!
# @file
#
# @created smohan 20170330

sp {topstate*propose*interaction*message
	(state <s> ^name troy
               ^io.input-link.interaction.message <message>
               ^interaction <inter>
               ^topstate <s>)
    (<message> -^processed true)
-->
	(<s> ^operator <op> +)
 	(<op> 	^name interaction
        	^type incoming-message
         	^message <message>
          	^interaction <inter>)
  }

sp {topstate*propose*interaction*status
	(state	<s>	^name troy
            	^interaction <inter>)
 	(<inter>	^processing-status <status>)
 	(<status> ^id <any-id>)
-->
	(<s>	^operator <op> +)
 	(<op>	^name interaction
        	^type new-processing-status
         	^processing-status <status>
          	^interaction <inter>)
  }

sp {topstate*propose*comprehension
    (state <s> ^name troy
               ^interaction.stack.segment <seg>)
    (<seg> ^message <m>
           ^groundings <g>)
    (<g> -^<any>)
    (<m> ^processed true
         ^lexical <lex>)
-->
    (<s> ^operator <op> +)
    (<op> ^name comprehension
          ^message <lex>
          ^segment <seg>)
    }

sp {topstate*propose*generation
	(state <s>	^name troy
            	^interaction <inter>)
 	(<inter> ^stack.segment <seg>)
 	(<seg>	^purpose <purpose-type>
         	^information-type <i-type>
          	-^response-generated true)
-->
	(<s>	^operator <op> +)
 	(<op>	^name generation)
   }


sp {topstate*propose*action
    (state <s> ^name troy
               ^interaction <inter>)
    (<inter>	^stack.segment <seg>
             	-^processing-status.<status> <any>)
    (<seg> ^groundings <g>
           -^status complete
            ^purpose execute)
    (<g> ^indexed-action <ia>)
-->
    (<s> ^operator <op> +)
    (<op> ^name action
          ^indexed-action <ia>
          ^segment <seg>)
    }

sp {topstate*propose*learning
    (state <s>    ^name troy
                  ^interaction <inter>)
    (<inter>    ^stack.segment <seg>
                -^processing-status.<status> <any>)
    (<seg>    ^groundings <g>
              ^purpose learn)
-->
    (<s>    ^operator <op> +)
    (<op>    ^name learning
             ^segment <seg>)
    }