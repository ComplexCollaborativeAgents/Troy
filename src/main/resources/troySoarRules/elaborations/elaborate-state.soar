##!
# @file
#
# @created smohan 20170331


sp {all*elaborate*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

sp {all*elaborate*topstate
	(state <s> ^superstate.topstate <ts>)
-->
	(<s> ^topstate <ts>)
 }

sp {all*elaborate*incoming-message
	(state <s>	^topstate <s>
            	^io.input-link.interaction.message <m1>
             	^interaction.incoming <inc>)
-->
	(<inc> ^message <m1>)
 }

sp {topstate*elaborate*world
	(state <s> ^topstate <s>
               ^io.input-link.world <w>)
-->
	(<s> ^world <w>)
 }

sp {all*elaborate*io
    (state <s> ^superstate.io <io>
               -^problemspace.learning-info explanation)
-->
    (<s> ^io <io>)
    }

sp {all*elaborate*interaction
	(state <s>	^superstate.interaction <i>
            	-^problemspace.world-info recreate)
-->
	(<s>	^interaction <i>)
 }
	

sp {all*elaborate*problemspace
	(state <s>	^impasse <any>)
-->
	(<s>	^problemspace <ps>)
 }

sp {all*elaborate*problemspace*learning-info
	(state <s>	^superstate.problemspace.learning-info explanation
				^problemspace <ps>)
-->
	(<ps>	^learning-info explanation)
}
sp {all*elaborate*problemspace*action-info
	(state <s>	^superstate.problemspace.action-info <any>
				^problemspace <ps>
    			^attribute operator)
-->
	(<ps>	^action-info <any>)
}

sp {topstate*elaborate*hack
	(state <s>	^topstate <s>)
-->
	(<s>	^hack <hack>)
 }

sp {all*elaborate*hack
	(state <s>	^superstate.hack <h>)
-->
	(<s>	^hack <h>)
 }

#### elaborate problem-space
sp {new-operator*elaborate*problem-space
   (state <s> ^name task)
-->  
   (<s> ^problem-space <p>)
   (<p> ^name task
   		^default-state-copy yes
		^two-level-attributes world
  		^one-level-attributes selected interaction)
}
            