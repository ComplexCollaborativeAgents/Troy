##!
# @file
#
# @created smohan 20170412
############################################################################
# Operator: smem lti retrieval operator which can be fired on any state
# If you add the wme ^retrieve-lti <lti> to ANY state, it will do the retrieval
#   of the substructure for epmem
# Will add a ^retrieved-lti <lti> to the state when done

sp {new*anystate*propose*smem-retrieve*create-cue
   (state <s> ^retrieve-lti <lti>
          -^retrieved-lti <lti>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name smem-retrieve
        ^type memory
        ^identifier <lti>)
}

sp {new*anystate*apply*smem-retrieve*create-cue
   (state <s> ^smem.command <cmd>
          ^operator <op>)
   (<op> ^name smem-retrieve
        ^identifier <lti>)
-->
   (<cmd> ^retrieve <lti>)
}

sp {new*anystate*apply*smem-retrieve*create-cue*clear
   (state <s> ^smem.command <cmd>
          ^operator <op>)
   (<op> ^name smem-retrieve
        ^identifier <lti>)
   (<cmd> ^{<command> << retrieve store query math-query >>} {<olti> <> <lti>})
-->
   (<cmd> ^<command> <olti> -)
}
   
sp {new*anystate*apply*smem-retrieve*copy-result*copy
   (state <s> ^operator <o>
              ^smem.result <sres>)
   (<sres> ^{ << success failure >>}  <lti>
           ^retrieved <nlti>)
   (<o> ^name smem-retrieve
        ^identifier <lti>)
-->
   (<s> ^retrieved-lti <nlti>)
}

sp {new*anystate*apply*smem-retrieve*copy-result*delete
   (state <s> ^operator <o>
              ^smem.result.{ << success failure >> }  <lti>
          ^smem.command <scomm>)
   (<o> ^name smem-retrieve
        ^identifier <lti>)
   (<scomm> ^retrieve <lti>)
-->
   (<scomm> ^retrieve <lti> -)
}