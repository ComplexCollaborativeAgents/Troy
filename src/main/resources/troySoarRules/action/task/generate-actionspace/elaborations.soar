##!
# @file
#
# @created smohan 20171030

sp {generate-actionspace*elaborate*map
	(state <s>	^name generate-actionspace
            	^superstate.superstate.operator <sop>)
 	(<sop>	^_metadata.map <map>)
-->
	(<s>	^map <map>)
 }

sp {generate-actionspace*retrieve*actionspace
	(state <s>	^name generate-actionspace
            	^map <map>)
 	(<map>	^actionspace <as>)
  	(<as> -^<any>)
-->
	(<s>	^retrieve-lti <as>)
 }

sp {generate-actionspace*elaborate*candidate-objects
	(state <s>	^name generate-actionspace
            	^map <map>)
-->
	(<s>	^candidate-objects <co>)
 }

sp {generate-actionspace*elaborate*candidate-objects*object
	(state <s>	^name generate-actionspace
            	^superstate.desired <d>
             	^candidate-objects <co>)
 	(<d>	^relation <rel>)
  	(<rel>	^<< e1 e2 >> <obj>)
-->
	(<co>	^object <obj>)
 }

sp {generate-actionspace*elaborate*candidate-operator*retrieve-id
	(state <s>	^name generate-actionspace
            	^map.actionspace._operator <pid>)
 	(<pid>	-^<any>)
-->
	(<s>	^retrieve-lti <pid>)
 }

sp {generate-actionspace*elaborate*candidate-operator*retrieve-id*arg
	(state <s>	^name generate-actionspace
            	^map.actionspace._operator.{<arg> <> action-type <> op_name} <pid>)
 	(<pid>	-^object <obj>)
-->
	(<s>	^retrieve-lti <pid>)
 }

sp {generate-actionspace*elaborate*candidate-operator*map
	(state <s>	^name generate-actionspace
            	^map.actionspace._subtask <submap>)
 	(<submap>	-^procedural <suboperator>)
-->
	(<s>	^retrieve-lti <submap>)
  }

sp {generate-actionspace*elaborate*candidate-operator*map*procedural
	(state <s>	^name generate-actionspace
            	^map.actionspace._subtask.procedural <proc>)
 	(<proc>	-^action-type complex)
-->
	(<s>	^retrieve-lti <proc>)
 }

sp {generate-actionspace*elaborate*candidate-operator*map*procedural*arguments
	(state <s>	^name generate-actionspace
            	^map.actionspace._subtask.procedural <proc>)
 	(<proc>	^{<arg> <> op_name <> action-type} <val>)
  	(<val> -^object <obj>)
-->
	(<s>	^retrieve-lti <val>)
 }

sp {generate-actionspace*elaborate*goal
	(state <s>	^name generate-actionspace
            	^map.actionspace._subtask.goal <gid>)
 	(<gid>	-^<any>)
-->
	(<s>	^retrieve-lti <gid>)
 }

sp {generate-actionspace*elaborate*goal*relation
	(state <s>	^name generate-actionspace
            	^map.actionspace._subtask.goal.relation <rid>)
 	(<rid>	-^<any>)
-->
	(<s>	^retrieve-lti <rid>)
 }


sp {generate-actionspace*elaborate*candidate-operator
	(state <s>	^name generate-actionspace
            	^map <map>
             	^candidate-objects <co>)
 	(<map> ^actionspace._subtask <submap>)
  	(<submap>	^procedural <pid>)
 	(<pid>	^op_name <name>
         	^action-type complex
          	^{<arg1> <> op_name <> action-type}	<val1>
			^{<arg2> <> <arg1> <> op_name <> action-type}.object <val2>)
	(<co>	^object <obj1>
       		^object {<obj2> <> <obj1>})
-->
	(<s>	^candidate-operator <cop>)
 	(<cop>	^name <name>
        	^<arg1>	<obj1>
         	^<arg2>.object <obj2>
          	^_metadata.map <submap>)
  }

sp {generate-actionspace*elaborate*candidate-operator*map*goal
	(state <s>	^name generate-actionspace
            	^retrieved-lti-cue.retrieved-lti <submap>)
 	(<submap>	^goal <gid>)
  	(<gid>	-^<any>)
-->
	(<s>	^retrieve-lti <gid>)
 }

sp {generate-actionspace*elaborate*candidate-operator*map*goal*rerahiot
	(state <s>	^name generate-actionspace
            	^retrieved-lti-cue.retrieved-lti.goal <goal>)
 	(<goal>	^relation <rid>)
  	(<rid>	-^<any>)
-->
	(<s>	^retrieve-lti <rid>)
 }

