##!
# @file
#
# @created smohan 20170407
sp {comprehension*propose*index-entity
    (state <s> ^name comprehension
               ^message <m>
               ^grounding <g>)
    (<m> ^entity <np>)
    - {(<g> ^entity <any>)
    (<any> ^_metadata.description <np>)}
-->
    (<s> ^operator <op> +)
    (<op> ^name index-entity
          ^description <np>)
}

sp {comprehension*propose*index-relation
	(state	<s>	^name comprehension
            	^message <m>
             	^grounding <g>)
 	(<m>	^relation <rel>)
   - {(<g> ^relation <any>)
    (<any> ^_metadata.description <rel>)}
-->
	(<s>	^operator <op> +)
 	(<op>	^name compose-relation
        	^description <rel>)
  }

sp {comprehension*propose*index-verb
    (state <s> ^name comprehension
               ^message <m>
               ^grounding <g>)
    (<m> ^verb <verb>)
    (<g> -^verb <any>)
-->
    (<s> ^operator <op> +)
    (<op> ^name index-verb
          ^description <m>)
    }

sp {comprehension*propose*index-action-object
	(state <s>	^name comprehension
            	^message <m>
             	^grounding <g>)
 	(<m> ^action-object <ao>)
  	- {(<g> ^action-object <any>)
    (<any> ^_metadata.description <ao>)}
-->
	(<s>	^operator <op> +)
 	(<op>	^name index-action-object
        	^description <ao>)
  }
        

sp {comprehension*propose*composition
    (state <s> ^name comprehension
               ^message <m>
               ^grounding <g>)
    -{(<m> ^{<attr> <> type <> processed} <val>)
      (<g> -^<attr>)}
-->
    (<s> ^operator <op> +)
    (<op> ^name composition
          ^message <m>
          ^grounding <g>)
    }