##!
# @file
#
# @created smohan 20170407
sp {index-noun-phrase*elaborate*description
    (state <s> ^name index-noun-phrase
               ^superstate.operator <sop>)
    (<sop> ^description <d>)
-->
    (<s> ^description <d>)
    }

sp {index-noun-phrase*elaborate*segment
    (state <s> ^name index-noun-phrase
               ^superstate <ss>)
    (<ss> ^segment <seg>)
-->
    (<s> ^segment <seg>)
    }


sp {index-noun-phrase*elaborate*resolution-type*set
    (state <s> ^name index-noun-phrase
               ^description <d>)
    (<d> ^determiner all)
-->
    (<s> ^resolution <res>)
    (<res> ^preference all
           ^arity multiple)
    }

sp {index-noun-phrase*elaborate*resolution-type*a
    (state <s> ^name index-noun-phrase
               ^description <d>)
    (<d> ^determiner a)
-->
    (<s> ^resolution <res>)
    (<res> ^preference equal
           ^arity single)
    }

sp {index-noun-phrase*elaborate*grounding
    (state <s> ^name index-noun-phrase)
-->
    (<s> ^grounding <g>)
    }

sp {index-noun-phrase*elaborate*segment*world
    (state <s> ^name index-noun-phrase
               ^resolution <res>
               ^grounding <g>
               ^segment.world-enhancements <w>)
    (<g> ^set <obj-set>)
    (<res> ^arity multiple)
-->
    (<w> ^set <obj-set>)
    }